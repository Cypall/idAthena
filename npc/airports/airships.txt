//===== rAthena Script ======================================= 
//= The Airship System Script
//===== Ditulis Oleh: ================================================== 
//= rAthena Dev Team
//===== Versi Saat Ini: =====================================
//= 1.1
//===== Kompatibel Dengan: ===================================== 
//= rAthena SVN
//===== Deskripsi: ========================================= 
//= Sistem pesawat terbang yang digunakan di server resmi.
//===== Additional Comments: ================================= 
//= 1.0 First version.
//= 1.1 Diterjemahkan ke dalam Bahasa Indonesia oleh nanakiwurtz
//============================================================ 
//= Domestic Airship Route
//= ... -> Juno -> Einbroch -> Lighthalzen -> Einbroch
//= -> Juno -> Hugel -> ...
//============================================================ 

airplane,243,73,0	script	#AirshipWarp-1	45,1,1,{
	end;

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc strnpcinfo(0);
	end;

OnUnhide:
	enablenpc strnpcinfo(0);
	specialeffect EF_SUMMONSLAVE;
	end;

OnTouch:
	switch ($@airplanelocation) {
	case 0: warp "yuno",92,260; end;
	case 1: warp "einbroch",92,278; end;
	case 2: warp "lighthalzen",302,75; end;
	case 3: warp "hugel",181,146; end;
	}
}

airplane,243,29,0	duplicate(#AirshipWarp-1)	#AirshipWarp-2	45,1,1

airplane,1,1,0	script	Domestic_Airship	-1,{
	end;
OnInit:
	initnpctimer;
	end;
OnTimer20000:
	mapannounce "airplane","Kita akan berangkat menuju Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane","Kita akan tiba di Einbroch sebentar lagi.",bc_map,"0x00ff00";
	end;
OnTimer60000:
	set $@airplanelocation,1;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Selamat datang di Einbroch. Selamat bersenang-senang!",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane","Sekarang kita ada di Einbroch. Pesawatnya akan berangkat sebentar lagi.",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","Pesawatnya sudah berangkat. Tujuan kita berikutnya adalah Lighthalzen.",bc_map,"0x70dbdb";
	end;
OnTimer100000:
	mapannounce "airplane","Kita akan berangkat menuju Lighthalzen.",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane","Kita akan tiba di Lighthalzen sebentar lagi.",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	set $@airplanelocation,2;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Selamat datang di Lighthalzen. Selamat bersenang-senang!",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane","Sekarang kita ada di Lighthalzen. Pesawatnya akan berangkat sebentar lagi.",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","Pesawatnya sudah berangkat. Tujuan kita berikutnya adalah Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer180000:
	mapannounce "airplane","Kita akan berangkat menuju Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer210000:
	mapannounce "airplane","Kita akan tiba di Einbroch sebentar lagi.",bc_map,"0x00FF00";
	end;
OnTimer220000:
	set $@airplanelocation,1;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Selamat datang di Einbroch. Selamat bersenang-senang!",bc_map,"0x00ff00";
	end;
OnTimer230000:
	mapannounce "airplane","Sekarang kita ada di Einbroch. Pesawatnya akan berangkat sebentar lagi.",bc_map,"0x00ff00";
	end;
OnTimer240000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","Pesawatnya sudah berangkat. Tujuan kita berikutnya adalah Juno.",bc_map,"0xff8200";
	end;
OnTimer260000:
	mapannounce "airplane","Kita akan berangkat menuju Juno.",bc_map,"0xff8200";
	end;
OnTimer290000:
	mapannounce "airplane","Kita akan tiba di Juno sebentar lagi.",bc_map,"0xff8200";
	end;
OnTimer300000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Selamat datang di Juno. Selamat bersenang-senang!",bc_map,"0xff8200";
	end;
OnTimer310000:
	mapannounce "airplane","Sekarang kita ada di Juno. Pesawatnya akan berangkat sebentar lagi.",bc_map,"0xff8200";
	end;
OnTimer320000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","Pesawatnya sudah berangkat. Tujuan kita berikutnya adalah Hugel.",bc_map,"0xca4bf3";
	end;
OnTimer340000:
	mapannounce "airplane","Kita akan berangkat menuju Hugel.",bc_map,"0xca4bf3";
	end;
OnTimer370000:
	mapannounce "airplane","Kita akan tiba di Hugel sebentar lagi.",bc_map,"0xca4bf3";
	end;
OnTimer380000:
	set $@airplanelocation,3;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Selamat datang di Hugel. Selamat bersenang-senang!",bc_map,"0xca4bf3";
	end;
OnTimer390000:
	mapannounce "airplane","Sekarang kita ada di Hugel. Pesawatnya akan berangkat sebentar lagi.",bc_map,"0xca4bf3";
	end;
OnTimer400000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","Pesawatnya sudah berangkat. Tujuan kita berikutnya adalah Juno.",bc_map,"0xff8200";
	end;
OnTimer420000:
	mapannounce "airplane","Kita akan berangkat menuju Juno.",bc_map,"0xff8200";
	end;
OnTimer450000:
	mapannounce "airplane","Kita akan tiba di Juno sebentar lagi.",bc_map,"0xff8200";
	end;
OnTimer460000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Selamat datang di Juno. Selamat bersenang-senang!",bc_map,"0xff8200";
	end;
OnTimer470000:
	mapannounce "airplane","Sekarang kita ada di Juno. Pesawatnya akan berangkat sebentar lagi.",bc_map,"0xff8200";
	end;
OnTimer480000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","Pesawatnya sudah berangkat. Tujuan kita berikutnya adalah Einbroch.",bc_map,"0x00ff00";
	stopnpctimer;
	initnpctimer;
}

airplane,240,64,5	script	Exit#airplane1a::ExitAirplane	857,{
	end;
}
airplane,247,64,5	duplicate(ExitAirplane)	Exit#airplane1b	857
airplane,240,40,1	duplicate(ExitAirplane)	Exit#airplane2a	857
airplane,247,40,1	duplicate(ExitAirplane)	Exit#airplane2b	857

airplane,100,69,3	script	Kru Pesawat#ein-1	852,{
	mes "[Kru Pesawat]";
	mes "Setelah pesawatnya sudah tiba di tempat tujuan, kamu bisa menggunakan tangga ke atas";
	mes "untuk meninggalkan pesawat ini.";
	mes "Terima kasih!";
	close;
}

airplane,64,94,1	script	Umbala Anak Kecil#ein_p	787,{
	emotion e_swt2;
	mes "[Anak Kecil]";
	if (event_umbala >= 3) {
		mes "Wow, ibu!";
		mes "Lihatlah!";
		mes "Kita sedang terbang! K-Kita sedang...";
		mes "Kita ada di atas awan!";
	}
	else {
		mes "Makumalagu!";
		mes "Saampa joojimbo";
		mes "kaku na jedi Solo.";
		mes "Bwahahahahahahaah!";
	}
	close;
}

airplane,66,93,3	script	Umbala Perempuan#ein_p	783,{
	emotion e_dots;
	mes "[Perempuan]";
	if (event_umbala >= 3) {
		mes "Sssttt...";
		mes "Anakku, diamlah~";
		mes "Jangan berteriak-teriak seperti itu saat kita ada di tempat umum!";
	}
	else {
		mes "Chooktu!";
		mes "Sacraup matii!";
		mes "Shaka gurftalfi";
		mes "huntiki manjoo!";
	}
	close;
}

airplane,71,91,7	script	Umbala Man#ein_p	789,{
	if (event_umbala >= 3) {
		mes "[Chrmlim]";
		mes "Halo~";
		mes "Kelihatannya kamu bisa mengerti bahasaku.";
		mes "^333333*Whew...!*^000000";
		next;
		emotion e_pif;
		mes "[Chrmlim]";
		mes "Kalau kamu tidak tahu, aku sudah membantu perusahaan pesawat terbang ini.";
		mes "Aku melatih para kru pesawat ini di Umbala untuk mengatasi perasaan takut mereka";
		mes "dengan cara loncat bungee. Menarik kan?";
		next;
		mes "[Chrmlim]";
		mes "Tapi...";
		mes "Beberapa dari mereka tidak bisa mengatasi rasa takutnya.";
		mes "Ada beberapa, um, yang berakhir di ^333333in Nifflheim^000000 malah...";
	}
	else {
		mes "[Chrmlim]";
		mes "Bajoo ga";
		mes "nukta Airship.";
		next;
		mes "[Chrmlim]";
		mes "...";
		mes "......";
		next;
		emotion e_pif;
		mes "[Chrmlim]";
		mes "Shabala moow bajama";
		mes "Airship kulaha googoona ";
		mes "salu. Dama, kookoo na nu";
		mes "yukuta. Um, fashuku na ret!";
	}
	close;
}

airplane,250,58,2	script	Staf Pesawat#airplane	67,{
	// Hugel quest addition
	if (hg_ma1 == 3) {
		mes "[Staf Pesawat]";
		mes "Selamat datang di pesawat ini!";
		mes "Ada yang bisa kubantu?";
		next;
		select("Apakah ada penumpang yang bernama Thierry?");
		mes "[Staf Pesawat]";
		mes "Maaf, kurasa tidak ada penumpang yang bernama Thierry.";
		close;
	}
	// Hugel quest end
	mes "[Staf Pesawat]";
	mes "Selamat datang di pesawat ini!";
	mes "Ada yang bisa kubantu?";
	next;
	switch (select("Aku ingin naik pesawat:Kabin Kapten:Fasilitas:Batal")) {
	case 1:
		mes "[Staf Pesawat]";
		mes "Kamu akan mendengar pemberitahuan bila pesawatnya sudah tiba di tujuan.";
		mes "Silakan gunakan salah satu pintu keluar, ada di sebelah Utara dan Selatan dari pesawat ini.";
		next;
		mes "[Staf Pesawat]";
		mes "Kalau tujuanmu sudah terlewat, jangan khawatir.";
		mes "Pesawat ini selalu mengudara di rute yang sudah ditentukan.";
		mes "Jadi pesawat ini pasti akan tiba di tempat tujuanmu lagi.";
		close;
	case 2:
		mes "[Staf Pesawat]";
		mes "Kabin Kapten ada di depan pesawat ini.";
		mes "Di sana kamu bisa bertemu dengan sang kapten dan sang pilot.";
		close;
	case 3:
		mes "[Staf Pesawat]";
		mes "Pesawat ini menyediakan beberapa Mini Games untuk menghibur para penumpang.";
		mes "Silakan coba keberuntungan dan keahlianmu di dalam Mini Games tersebut~";
		close;
	case 4:
		mes "[Staf Pesawat]";
		mes "Terima kasih, dan selamat bersenang-senang!";
		close;
	}
}

airplane,80,71,2	script	Zerta#01airplane	834,{
	mes "[Zerta]";
	mes "Oh, halo!";
	mes "Sekarang aku sedang menjalani perjalanan suci,";
	mes "aku berdoa untuk kemajuan dan kesejahteraan Rune-Midgard.";
	close;
}

airplane,65,63,4	script	Maelin#01airplane	714,{
	mes "[Maelin]";
	mes "Um, pesawat ini sedang menuju ke Lutie, iya kan?";
	mes "Aku sudah menunggu lama, tapi aku belum mendengar pemberitahuan tentang Lutie.";
	close;
}

airplane,72,34,6	script	Aanos#01airplane	702,{
	mes "[Aanos]";
	mes "Oh wooow~";
	mes "Langitnya terlihat begitu berbeda dan sangat indah dari atas sini!";
	close;
}

airplane,221,158,2	script	Pilot#airplane	852,{
	// Hugel quest addition
	if (hg_ma1 == 3) {
		mes "[Pilot]";
		mes "Kuharap aku bisa meminum bir dingin yang segar.";
		mes "Minum bir adalah tujuan hidupku! Minum bir memberiku energi!";
		mes "Aku tidak bisa hidup tanpa bir!";
		next;
		mes "[Pilot]";
		mes "Tapi! Menjalankan pesawat saat mabuk, sangatlah tidak baik.";
		mes "Tapi! Itu membuatku ingin minum bir lagi dan lagi!";
		emotion e_sob;
		next;
		select("Apakah ada penumpang yang bernama Thierry?");
		mes "[Pilot]";
		mes "Seragam ini sangat cocok dengan warna pesawat ini.";
		next;
		mes "[Pilot]";
		mes "...";
		mes "......";
		mes "Belum ada yang pernah datang ke ruangan ini sebelumnya.";
		mes "Si kapten adalah seekor rusa. Aku ingin mengenakan celana dalamku...";
		next;
		emotion e_omg;
		mes "[Pilot]";
		mes "Wah!? Siapa kamu!";
		next;
		mes "- ...Kelihatannya dia tidak mendengarkanmu sama sekali -";
		close;
	}
	// Hugel quest end
	switch (rand(1,4)) {
	case 1:
		mes "[Pilot]";
		mes "Sudah lama sekali sejak aku bisa menikmati bir dingin yang nikmat.";
		mes "Tapi pekerjaanku menuntutku agar aku selalu sadar setiap waktu!";
		next;
		mes "[Pilot]";
		mes "Jangan pernah mabuk sembarangan!";
		mes "Tapi aku masih ingat, saat terakhir kali aku pergi berlibur.";
		mes "Yeap, sedikit bir, keripik kentang, TV. Rasanya hidup indah sekali!";
		emotion e_sob;
		close;
	case 2:
		mes "[Pilot]";
		mes "Oooh cuacanya sangat bagus hari ini.";
		mes "Sangat cerah, cocok untuk menjalankan pesawat dengan santai.";
		close;
	case 3:
		mes "[Pilot]";
		mes "kamu tahu, kapten kami adalah seorang yang dihormati.";
		mes "Dia dan adiknya adalah orang yang sudah dikenal di bisnis ini.";
		next;
		mes "[Pilot]";
		mes "Hanya antara kita berdua, aku akan memberitahumu sesuatu.";
		mes "Santa itu sepertinya menyembunyikan sesuatu.";
		mes "Dia memperkerjakan rusa dan para elf.";
		mes "Dia sangat jenius!";
		close;
	default:
		mes "[Pilot]";
		mes "Kamu tahu, cara mengemudikan pesawat ini masih terasa aneh.";
		mes "Mungkin karena mereka membuat pesawat ini dengan terburu-buru.";
		next;
		emotion e_omg;
		mes "[Pilot]";
		mes "Tetapi mereka benar-benar serius, mereka sudah berpikir ke depan.";
		mes "Maksudku, mereka sudah melatih para kru saat pesawatnya masih dalam tahap pengembangan.";
		mes "Gila kan?!";
		close;
	}
}

airplane,50,66,5	script	Apple Merchant#airplane	86,{
	mes "[Fruitz]";
	mes "Selamat datang di toko Fruitz.";
	mes "Kamu bisa membeli Apel atau memprosesnya menjadi Jus Apel.";
	next;
	switch (select("Beli Apel.:Buat Jus Apel.:Kenapa kamu ada di sini?:Batal.")) {
	case 1:
		mes "[Fruitz]";
		mes "Silakan tulis jumlah Apel yang ingin kamu beli.";
		mes "Harganya 15 zeny per buah, dan kamu bisa membeli sampai 500 buah.";
		mes "Ketik '0' untuk membatalkan.";
		next;
		while (1) {
			input .@input;
			set .@pay, .@input * 15;
			if (.@input == 0) {
				mes "[Fruitz]";
				mes "Terima kasih telah datang ke tokoku.";
				mes "Kapan-kapan datang lagi ya~";
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes "[Fruitz]";
				mes "Kamu hanya bisa membeli 500 buah per transaksi.";
				next;
			}
			else {
				mes "[Fruitz]";
				mes "Harga ^FF0000" + .@input + "^000000 buah Apel adalah ";
				mes "^FF0000" + .@pay + " ^000000 zeny.";
				mes "Apakah kamu ingin membelinya?";
				next;
				if (select("Ya:Tidak") == 2) {
					mes "[Fruitz]";
					mes "Terima kasih telah datang ke tokoku.";
					mes "Kapan-kapan datang lagi ya~";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[Fruitz]";
			mes "Maaf, tapi kamu tidak mempunyai uang yang cukup.";
			mes "Silakan periksa dulu uangmu, atau belilah dalam jumlah yang lebih kecil.";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[Fruitz]";
			mes "Hmmm, sepertinya kamu tidak mempunyai ruang yang cukup di dalam tasmu.";
			close;
		}
		else {
			set Zeny, Zeny - .@pay;
			getitem 512,.@input; //Apple
			mes "[Fruitz]";
			mes "Terima kasih telah datang ke tokoku.";
			mes "Kuharap kamu suka Apelnya~!";
			close;
		}
	case 2:
		mes "[Fruitz]";
		mes "Oke, aku butuh ";
		mes "^FF00003 Apel dan 1 Empty Bottle^000000";
		mes "untuk membuat 1 Jus Apel.";
		mes "Apakah kamu ingin membuat Jus Apel?";
		next;
		switch (select("Ya:Tidak")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[Fruitz]";
				mes "Maaf, kamu tidak mempunyai bahan yang cukup untuk membuat Jus Apel.";
				mes "Ingat, aku butuh 3 Apel dan 1 Empty Bottle.";
				close;
			}
			else {
				mes "[Fruitz]";
				mes "Terima kasih, silakan tunggu sebentar.";
				next;
				mes "^3355FF*Peras peras*";
				mes "*Peras peras*";
				mes "*Clang...!*^000000";
				next;
				delitem 512,3; //Apple
				delitem 713,1; //Empty_Bottle
				getitem 531,1; //Apple_Juice
				mes "[Fruitz]";
				mes "Silakaaaan~";
				mes "Selamat menikmati!";
				mes "Silakan datang lagi kalau kamu ingin Jus Apel lagi ya!";
				close;
			}
		case 2:
			mes "[Fruitz]";
			mes "Terima kasih telah datang ke tokoku.";
			mes "Kapan-kapan datang lagi ya~";
			close;
		}
	case 3:
		mes "[Fruitz]";
		mes "Dulu aku adalah seorang pengelana.";
		mes "Suatu hari, saat aku sedang tidur siang, sesuatu mengenai kepalaku dan membangunkanku.";
		next;
		mes "[Fruitz]";
		mes "Ternyata aku sedang tidur di bawah pohon apel, dan sebuah apel jatuh di atas kepalaku.";
		mes "Saat itu aku sedang kelaparan dan hampir saja memakan apel tadi...";
		next;
		mes "[Fruitz]";
		mes "Tapi tiba-tiba Kain, teman lamaku dari pertambangan memintaku untuk membantunya di dalam pesawat.";
		mes "Jadi aku membantunya di dalam pesawat dan aku melihat beberapa orang yang sedang bermain dadu.";
		next;
		mes "[Fruitz]";
		mes "Aku sedang bosan dan penasaran, dan akhirnya aku malah ikut main.";
		mes "Aku tidak tahu mengapa aku bisa begitu beruntung!";
		mes "Hadiahnya bertambah besar, dari satu apel jadi dua...";
		next;
		mes "[Fruitz]";
		mes "Dua jadi empat, dan sebelum aku menyadarinya, aku sudah mengumpulkan banyak sekali apel!";
		mes "Jadi aku mulai menjalankan bisnisku sendiri di dalam pesawat ini dengan menjual apel.";
		mes "Cukup aneh ya?";
		next;
		mes "[Fruitz]";
		mes "Jadi, apel sangat bagus untuk kesehatanmu.";
		mes "Aku juga sangat menyukainya. Hahahahaah~!";
		close;
	case 4:
		mes "[Fruitz]";
		mes "Terima kasih telah datang ke tokoku.";
		mes "Kapan-kapan datang lagi ya~";
		close;
	}
}

//============================================================ 
//= International Airship Route
//= ... -> Izlude -> Juno -> Rachel -> ...
//============================================================ 

airplane_01,243,73,0	script	#AirshipWarp-3	45,1,1,{
	end;
	
OnTouch:
	switch ($@airplanelocation2) {
	case 0: warp "ra_fild12",292,204; end;
	case 1: 
		if(checkre(0))
			warp "izlude",200,73;
		else
			warp "izlude",200,56;
		end;
	case 2: warp "yuno",12,261; end;
	}

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc strnpcinfo(0);
	end;
	
OnUnhide:
	enablenpc strnpcinfo(0);
	specialeffect EF_SUMMONSLAVE;
	end;
}

airplane_01,243,29,0	duplicate(#AirshipWarp-3)	#AirshipWarp-4	45,1,1

airplane_01,1,1,0	script	International_Airship	-1,{
	end;
OnInit:
OnEnable:
	initnpctimer;
	end;
OnTimer25000:
	mapannounce "airplane_01","Kita akan berangkat menuju Izlude.",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane_01","Kita akan tiba di Izlude sebentar lagi.",bc_map,"0x00ff00";
	end;
OnTimer60000:
	set $@airplanelocation2,1;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","Selamat datang di Izlude. Selamat bersenang-senang!",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane_01","Sekarang kita ada di Izlude. Pesawatnya akan berangkat sebentar lagi.",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","Pesawatnya sudah berangkat. Tujuan kita berikutnya adalah Juno.",bc_map,"0x70dbdb";
	end;
OnTimer105000:
	mapannounce "airplane_01","Kita akan berangkat menuju Juno.",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane_01","Kita akan tiba di Juno sebentar lagi.",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	set $@airplanelocation2,2;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","Selamat datang di Juno. Selamat bersenang-senang!",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane_01","Sekarang kita ada di Juno. Pesawatnya akan berangkat sebentar lagi.",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","Pesawatnya sudah berangkat. Tujuan kita berikutnya adalah Rachel.",bc_map,"0xFF8200";
	end;
OnTimer185000:
	mapannounce "airplane_01","Kita akan berangkat menuju Rachel.",bc_map,"0xFF8200";
	end;
OnTimer210000:
	mapannounce "airplane_01","Kita akan tiba di Rachel sebentar lagi.",bc_map,"0xFF8200";
	end;
OnTimer220000:
	set $@airplanelocation2,0;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","Selamat datang di Rachel. Selamat bersenang-senang!",bc_map,"0xFF8200";
	end;
OnTimer230000:
	mapannounce "airplane_01","Sekarang kita ada di Rachel. Pesawatnya akan berangkat sebentar lagi.",bc_map,"0xFF8200";
	end;
OnTimer240000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","Pesawatnya sudah berangkat. Tujuan kita berikutnya adalah Izlude.",bc_map,"0x00ff00";
	stopnpctimer;
	set .moninv, .moninv + 1;
	if (.moninv == 7) {
		if (rand(1,3) == 3) {
			donpcevent "Airship#airplane02::OnEnable";
			end;
		}
		set .moninv, 0;
	}
	initnpctimer;
	end;
}

airplane_01,240,64,5	script	Exit#airplane_011a::ExitAirplane01	857,{
	end;
}
airplane_01,247,64,5	duplicate(ExitAirplane01)	Exit#airplane_011b	857
airplane_01,240,40,1	duplicate(ExitAirplane01)	Exit#airplane_012a	857
airplane_01,247,40,1	duplicate(ExitAirplane01)	Exit#airplane_012b	857

airplane_01,250,58,2	script	Staf Pesawat#airplane01	67,{
	mes "[Staf Pesawat]";
	mes "Welcome";
	mes "Selamat datang di pesawat ini!";
	mes "Ada yang bisa kubantu?";
	next;
	switch (select("Aku ingin naik pesawat:Kabin Kapten:Fasilitas:Batal")) {
	case 1:
		mes "[Staf Pesawat]";
		mes "Kamu akan mendengar pemberitahuan bila pesawatnya sudah tiba di tujuan.";
		mes "Silakan gunakan salah satu pintu keluar, ada di sebelah Utara dan Selatan dari pesawat ini.";
		next;
		mes "[Staf Pesawat]";
		mes "Kalau tujuanmu sudah terlewat, jangan khawatir.";
		mes "Pesawat ini selalu mengudara di rute yang sudah ditentukan.";
		mes "Jadi pesawat ini pasti akan tiba di tempat tujuanmu lagi.";
		close;
	case 2:
		mes "[Staf Pesawat]";
		mes "Kabin Kapten ada di depan pesawat ini.";
		mes "Di sana kamu bisa bertemu dengan sang kapten dan sang pilot.";
		close;
	case 3:
		mes "[Staf Pesawat]";
		mes "Pesawat ini menyediakan beberapa Mini Games untuk menghibur para penumpang.";
		mes "Silakan coba keberuntungan dan keahlianmu di dalam Mini Games tersebut~";
		close;
	case 4:
		mes "[Staf Pesawat]";
		mes "Terima kasih, dan selamat bersenang-senang!";
		close;
	}
}

airplane_01,50,66,5	script	Apple Merchant#air01	86,{
	if (checkweight(1201,1) == 0) {
		mes "- Tunggu sebentar !! -";
		mes "- Saat ini kamu membawa -";
		mes "- terlalu banyak barang. -";
		mes "- Harap coba lagi setelah -";
		mes "- kamu menyimpan beberapa barang. -";
		close;
	}
	mes "[Meltz]";
	mes "Selamat datang di toko Meltz.";
	mes "Kamu bisa membeli Apel atau memprosesnya menjadi Jus Apel.";
	next;
	switch (select("Beli Apel.:Buat Jus Apel.:Kenapa kamu ada di sini?:Batal.")) {
	case 1:
		mes "[Meltz]";
		mes "Silakan tulis jumlah Apel yang ingin kamu beli.";
		mes "Harganya 15 zeny per buah, dan kamu bisa membeli sampai 500 buah.";
		mes "Ketik '0' untuk membatalkan.";
		next;
		while (1) {
			input .@input;
			set .@pay, .@input * 15;
			if (.@input == 0) {
				mes "[Meltz]";
				mes "Terima kasih telah datang ke tokoku.";
				mes "Kapan-kapan datang lagi ya~";
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes "[Meltz]";
				mes "Kamu hanya bisa membeli 500 buah per transaksi.";
				next;
			}
			else {
				mes "[Meltz]";
				mes "Harga ^FF0000" + .@input + "^000000 buah Apel adalah ";
				mes "^FF0000" + .@pay + " ^000000 zeny.";
				mes "Apakah kamu ingin membelinya?";
				next;
				if (select("Ya:Tidak") == 2) {
					mes "[Meltz]";
					mes "Terima kasih telah datang ke tokoku.";
					mes "Kapan-kapan datang lagi ya~";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[Meltz]";
			mes "Maaf, tapi kamu tidak mempunyai uang yang cukup.";
			mes "Silakan periksa dulu uangmu, atau belilah dalam jumlah yang lebih kecil.";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[Meltz]";
			mes "Hmmm, sepertinya kamu tidak mempunyai ruang yang cukup di dalam tasmu.";
			close;
		}
		else {
			set Zeny, Zeny - .@pay;
			getitem 512,.@input; //Apple
			mes "[Meltz]";
			mes "Terima kasih telah datang ke tokoku.";
			mes "Kuharap kamu suka Apelnya~!";
			close;
		}
	case 2:
		mes "[Meltz]";
		mes "Oke, aku butuh ";
		mes "^FF00003 Apel dan 1 Empty Bottle^000000";
		mes "untuk membuat 1 Jus Apel.";
		mes "Apakah kamu ingin membuat Jus Apel?";
		next;
		switch (select("Ya:Tidak")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[Meltz]";
				mes "Maaf, kamu tidak mempunyai bahan yang cukup untuk membuat Jus Apel.";
				mes "Ingat, aku butuh 3 Apel dan 1 Empty Bottle.";
				close;
			}
			else {
				mes "[Meltz]";
				mes "Terima kasih, silakan tunggu sebentar.";
				next;
				mes "^3355FF*Peras peras*";
				mes "*Peras peras*";
				mes "*Clang...!*^000000";
				next;
				delitem 512,3; //Apple
				delitem 713,1; //Empty_Bottle
				getitem 531,1; //Apple_Juice
				mes "[Meltz]";
				mes "Silakaaaan~";
				mes "Selamat menikmati!";
				mes "Silakan datang lagi kalau kamu ingin Jus Apel lagi ya!";
				close;
			}
		case 2:
			mes "[Meltz]";
			mes "Terima kasih telah datang ke tokoku.";
			mes "Kapan-kapan datang lagi ya~";
			close;
		}
	case 3:
		mes "[Meltz]";
		mes "Terima kasih telah datang ke tokoku.";
		mes "Kapan-kapan datang lagi ya~";
		close;
	}
}

airplane_01,221,158,2	script	Pilot#airplane_01	852,{
	switch (rand(1,4)) {
	case 1:
		mes "[Pilot]";
		mes "Garis lintang, sudut 131 Timur.";
		mes "Garis bujur, sudut 37 Utara.";
		mes "Kita ada di jalur yang tepat, kapten.";
		close;
	case 2:
		mes "[Pilot]";
		mes "Kelihatannya hari ini";
		mes "sangat berawan. Sangat sulit untuk";
		mes "bernavigasi bila langitnya tidak cerah.";
		mes "Sepertinya kita harus memperkuat radarnya";
		close;
	case 3:
		mes "[Pilot]";
		mes "Si Kapten adalah seorang yang baik";
		mes "dan kurasa tidak ada orang yang lebih baik";
		mes "daripada dia dalam hal mengendalikan pesawat ini.";
		mes "Tapi dia sangat tegas dalam hal pekerjaan.";
		next;
		mes "[^ff0000Tarlock^000000]";
		mes "^ff0000Hey...!^000000";
		mes "^ff0000Jangan mengobrol terus!^000000";
		mes "^ff0000Teruslah bekerja!^000000";
		next;
		mes "[Pilot]";
		mes "B-baik, pak!";
		mes "(Lihat kan?)";
		close;
	default:
		mes "[Pilot]";
		mes "Seragam ini sangat cocok dengan warna pesawat ini.";
		next;
		mes "[Pilot]";
		mes "...";
		mes "......";
		mes "Belum ada yang pernah datang ke ruangan ini sebelumnya.";
		mes "Si kapten adalah seekor rusa. Aku ingin mengenakan celana dalamku...";
		next;
		emotion e_omg;
		mes "[Pilot]";
		mes "Oh...! Halo!";
		mes "A-a-apakah kamu menikmati perjalanan ini?!";
		close;
	}
}

airplane_01,83,61,2	script	Dianne#01airplane_01	72,2,2,{
	mes "[Dianne]";
	mes "Rasanya sangat aneh!";
	mes "Aku pergi ke kabin kapten, tapi yang kulihat";
	mes "hanyalah rusa aneh ini.";
	mes "Oh!";
	mes "Apakah dia adalah...";
	close;

OnTouch:
	emotion e_sob;
	end;
}

airplane_01,69,63,2	script	Mendel#01airplane_01	55,{
	mes "[Mendel]";
	mes "Seperti yang kuduga, makanan di pesawat ini";
	mes "hanya bernilai bintang 3!";
	mes "*Harrrumph* Seharusnya aku membawa";
	mes "kokiku sendiri supaya aku bisa makan";
	mes "makanan yang lebih enak.";
	close;
}

airplane_01,71,31,2	script	Swordsman Shimizu#air_01	106,{
	mes "[Swordsman Shimizu]";
	mes "Akhirnya, setelah lima tahun menunggu...";
	mes "Aku bisa membalaskan dendamku!";
	next;
	mes "[Swordsman Shimizu]";
	mes "Aku hanya...";
	mes "Harus memastikan bahwa aku";
	mes "tidak melewatkan lokasi pemberhentiannya lagi.";
	mes "Sebentar lagi, sebentar lagi aku bisa";
	mes "membalaskan dendamku!";
	close;
}

//============================================================ 
//= Tantangan Mengetik
//============================================================ 
airplane_01,32,61,4	script	Nils#ein	49,1,1,{
	mes "[Nils]";
	mes "Selamat datang di";
	mes "^ff0000Tantangan Mengetik RO^000000.";
	mes "Apakah kamu ingin bermain?";
	next;
	switch (select("Main ^ff0000Tantangan Mengetik RO^000000:Informasi:Lihat Peringkat Terbaik:Batal")) {
	case 1:
		mes "[Nils]";
		mes "Okay, kita kedatangan penantang baru!";
		mes "Ketiklah kalimat di bawah ini dengan";
		mes "cepat tanpa salah!";
		mes "Ayo mulai~!";
		setarray .@line1_1$[0],	"^3cbcbccallipygian salacius lascivious^000000",
					"^3cbcbcBy the power of^000000",
					"^0000ffthkelfkskeldmsiejdlslehfndkelsheidl^000000",
					"^3cbcbcburrdingdingdingdilidingdingdingphoohudaamb^000000",
					"^3cbcbcCoboman no chikara-yumei na^000000",
					"^3cbcbcI'm the king of All Weirdos! Now^000000",
					"^3cbcbcYou give me no choice. I guess it's^000000";
		setarray .@line1_2$[0],	"^3cbcbclicentious prurient concupiscent^000000",
					"^3cbcbcp-po-poi-po-poi-poin-poing^000000",
					"^3cbcbcskemd^000000",
					"^3cbcbcdanoorabambarambambambambamburanbamding^000000",
					"^3cbcbcchikara-daiookii na chikara da ze!^000000",
					"^3cbcbcyou know of my true power. Obey~!^000000",
					"^3cbcbctime for me to reveal my secret...^000000";
		setarray .@line1_3$[0],	"",
					"^3cbcbcGOD-POING. I NEVER LOSE!^000000",
					"",
					"",
					"^3cbcbcCOBO ON^000000",
					"",
					"";
		setarray .@word1$[0],	"callipygian salacius lascivious licentious prurient concupiscent",
					"By the power of p-po-poi-po-poi-poin-poing GOD-POING. I NEVER LOSE!",
					"thkelfkskeldmsiejdlslehfndkelsheidlskemd",
					"burrdingdingdingdilidingdingdingphoohudaambdanoorabambarambambambambamburanbamding",
					"Coboman no chikara-yumei na chikara-daiookii na chikara da ze! COBO ON",
					"I'm the king of All Weirdos! Now you know of my true power. Obey~!",
					"You give me no choice. I guess it's time for me to reveal my secret...";
		setarray .@line2_1$[0],	"^3cbcbcuNflAPPaBLe LoVaBLe SeCreTs and^000000",
					"^ff1493LiGhTsPeEd RiGhT SPEed LeFT TURn^000000",
					"^ff1493hfjdkeldjsieldjshfjdjeiskdlefvbd^000000",
					"^ff1493burapaphuraldaneramduanbatuhiwooi^000000",
					"^ff1493belief love luck grimace sweat rush^000000",
					"^800080opeN, Open!op3n.openOpen0p3nOpEn0pen^000000",
					"^3cbcbcfReeDoM ecstAcy JoUrnaliSm ArMplt^000000";
		setarray .@line2_2$[0],	"^3cbcbcboWLIiNg aGaINST tHe KarMA of YoUtH^000000",
					"^ff1493RiGhT BuRn OrIGInAL GaNgSteR SmACk^000000",
					"",
					"^ff1493kabamturubamdingding^000000",
					"^ff1493folktale rodimus optimus bumblebee^000000",
					"^800080`open'0Pen open? open!111OPENSESAME^000000",
					"^3cbcbcDisCoverY hEaDaChE MoonbeAmS jUsTiCE^000000";
		setarray .@word2$[0],	"uNflAPPaBLe LoVaBLe SeCreTs and boWLIiNg aGaINST tHe KarMA of YoUtH",
					"LiGhTsPeEd RiGhT SPEed LeFT TURn RiGhT BuRn OrIGInAL GaNgSteR SmACk",
					"hfjdkeldjsieldjshfjdjeiskdlefvbd",
					"burapaphuraldaneramduanbatuhiwooikabamturubamdingding",
					"belief love luck grimace sweat rush folktale rodimus optimus bumblebee",
					"opeN, Open!op3n.openOpen0p3nOpEn0pen`open'0Pen open? open!111OPENSESAME",
					"fReeDoM ecstAcy JoUrnaliSm ArMplt DisCoverY hEaDaChE MoonbeAmS jUsTiCE";
		setarray .@letters[0],	1300,
					1250,
					1180,
					1380,
					1740,
					1440,
					1450;
		set .@wordtest, rand(7);
		next;
		mes "[Nils]";
		mes .@line1_1$[.@wordtest];
		mes .@line1_2$[.@wordtest];
		mes .@line1_3$[.@wordtest];
		set .@start_time, gettime(3)*60*60 + gettime(2)*60 + gettime(1);
		next;
		input .@save1$;
		set .@end_time, gettime(3)*60*60 + gettime(2)*60 + gettime(1);
		set .@total_time, .@end_time - .@start_time;
		mes "[Nils]";
		mes .@line2_1$[.@wordtest];
		mes .@line2_2$[.@wordtest];
		set .@start_time, gettime(3)*60*60 + gettime(2)*60 + gettime(1);
		next;
		input .@save2$;
		set .@end_time, gettime(3)*60*60 + gettime(2)*60 + gettime(1);
		set .@total_time, .@total_time + (.@start_time - .@end_time);
		set .@tasoo, (.@letters[.@wordtest] / .@total_time) * 6;
		if ((.@save1$ == .@word1$[.@wordtest]) && (.@save2$ == .@word2$[.@wordtest])) {
			mes "[Nils]";
			mes "Rekormu adalah ^ff0000" + .@total_time + " detik^000000 dan";
			mes "jumlah hurufnya adalah " + .@tasoo + ".";
			next;
			if (.@tasoo >= 1300) {
				mes "[Nils]";
				mes "Hmmm, hasil ini sangat tidak mungkin untuk orang normal!";
				mes "berhentilah melakukan copy-paste dan bermainlah dengan jujur lain kali.";
				close;
			}
			if (.@tasoo >= $050320_ein_typing) {
				mes "[Nils]";
				mes "Rekor terbaik sebelumnya ";
				mes "dipegang oleh ^0000ff" + $050320_minus1_typing$ + "^000000";
				mes "dengan jumlah ^0000ff" + $050320_ein_typing + "^000000 huruf.";
				mes "Tetapi, ^ff0000" + strcharinfo(0) + "^000000,";
				mes "kamu telah membuat sebuah rekor baru.";
				mes "Selamat!";
				set $050320_minus1_typing$, strcharinfo(0);
				set $050320_ein_typing, .@tasoo;
				close;
			}
			else {
				mes "[Nils]";
				mes "^0000ff" + $050320_minus1_typing$ + "^000000";
				mes "adalah pemegang rekor saat ini";
				mes "dengan total ^0000ff" + $050320_ein_typing + "^000000 huruf";
				mes "Cobalah kalahkan rekor ini lain kali~";
				close;
			}
		}
		else {
			mes "[Nils]";
			mes "Oooh...";
			mes "Maafkan aku, tapi kamu telah melakukan kesalahan saat mengetik...";
			close;
		}
	case 2:
		mes "[Nils]";
		mes "^ff0000Tantangan Mengetik RO^000000";
		mes "adalah sebuah permainan di mana para pemain";
		mes "harus mengetik kalimat yang diberikan secepat mungkin.";
		mes "Nama dari pemegang rekor akan kusimpan.";
		mes "Kalau kamu ingin terkenal, inilah kesempatanmu!";
		next;
		mes "[Nils]";
		mes "Aku akan memberitahumu, semua kalimat"
		mes "harus diketik dalam 1 kotak, jadi jangan menekan tombol Enter.";
		mes "Tapi gunakanlah tombol 'OK'.";
		close;
	case 3:
		mes "[Nils]";
		mes "^0000ff" + $050320_minus1_typing$ + "^000000";
		mes "adalah pemegang rekor tertinggi saat ini";
		mes "dengan jumlah ^0000ff" + $050320_ein_typing + "^000000 huruf";
		mes "Cobalah kalahkan rekor ini lain kali~";
		close;
	case 4:
		mes "[Nils]";
		mes "Silakan datang lagi kalau kamu ingin bermain";
		mes "^ff0000Tantangan Mengetik RO^000000.";
		mes "Aku akan selalu menunggumu di sini~";
		close;
	}
}

//============================================================ 
//= Apple Gambling
//============================================================ 
airplane_01,33,68,4	script	Clarice	74,{
	mes "[Clarice]";
	mes "Hai, namaku Clarice~";
	mes "Apakah kamu ingin bertaruh beberapa buah Apel";
	mes "untuk bermain dalam permainan dadu?";
	next;
	callfunc "applegamble","Clarice";
	end;
}

function	script	applegamble	{

	switch (select("Play Dice Game:Learn Dice Game Rules:Cancel")) {
	case 3:
		mes "["+getarg(0)+"]";
		mes "Aku siap menemanimu bermain dadu setiap saat.";
		mes "Bicaralah denganku bila kamu ingin bermain, oke?";
		close;
	case 2:
		mes "["+getarg(0)+"]";
		mes "Peraturannya sangat mudah. Pertama,";
		mes "tentukan jumlah Apel yang akan kita mainkan.";
		mes "Kita bisa bermain sampai 50 buah Apel untuk 1 ronde.";
		mes "Supaya tidak melawan hukum, aku hanya menerima Apel.";
		next;
		mes "["+getarg(0)+"]";
		mes "Tapi, kalau kamu punya banyak zeny";
		mes "dan ingin menghabiskannya,";
		mes "datanglah ke toko Fruitz";
		mes "dan kamu bisa membeli Apel yang sebanyak kamu mau!";
		next;
		mes "["+getarg(0)+"]";
		mes "Cara bermainnya, kita lempar";
		mes "2 buah dadu yang bersisi 6.";
		mes "Saat giliranmu, kamu bisa";
		mes "melempar 2 buah dadu juga. Setelah";
		mes "itu kita diberi kesempatan untuk melempar";
		mes "dadu ketiga.";
		next;
		mes "["+getarg(0)+"]";
		mes "nah ini yang penting, kalau jumlah angkamu";
		mes "lebih dari 12, kamu akan kalah.";
		mes "Jadi kita tidak boleh melebihi angka 12.";
		mes "Mengerti?";
		next;
		mes "["+getarg(0)+"]";
		mes "Kamu diberi kesempatan untuk melempar dadu";
		mes "ketiga atau tidak.";
		mes "Aku juga begitu, aku mungkin akan melempar dadu ketiga,";
		mes "bisa juga tidak.";
		next;
		mes "["+getarg(0)+"]";
		mes "Kalau kamu menang,";
		mes "kamu akan menerima Apel dua kali lipat";
		mes "dari jumlah awal.";
		mes "Tapi kalau jumlah kita sama,";
		mes "aku akan mengembalikan semua Apelmu lagi.";
		mes "Cukup adil kan?";
		close;
	case 1:
		break;
	}
	mes "["+getarg(0)+"]";
	mes "Ooh, kamu ingin bermain denganku?";
	mes "Bagus! Berapa banyak Apel yang ingin kamu mainkan?";
	mes "Aku hanya menerima 50 Apel tiap ronde.";
	mes "Kalau kamu ingin membatalkan, ketik '0'.";
	next;
	while(1) {
		input .@amount;
		if (.@amount == 0) {
			mes "["+getarg(0)+"]";
			mes "Tidak jadi?";
			mes "Aku mengerti. Kuharap kita bisa bermain lagi nanti,";
			close;
		}
		else if (.@amount < 1 || .@amount > 50) {
			mes "["+getarg(0)+"]";
			mes "Kamu tidak boleh bermain dengan Apel lebih dari";
			mes "50 buah.";
			mes "Ketik jumlah Apel kurang dari 50.";
			next;
			continue;
		}
		mes "["+getarg(0)+"]";
		mes "Jadi kamu ingin bermain dengan";
		mes "^FF0000"+.@amount+"^000000 Apel.";
		mes "Apakah sudah benar?";
		next;
		if (select("Ya:Tidak") == 2) {
			mes "["+getarg(0)+"]";
			mes "Mm, salah ya?";
			mes "Baiklah, silakan tulis kembali jumlah Apel yang ingin kamu mainkan.";
			next;
			continue;
		}
		if (countitem(512) <.@amount) {
			mes "Maaf, tapi kamu tidak punya Apel sebanyak itu.";
			next;
			continue;
		}
		delitem 512,.@amount;
		mes "["+getarg(0)+"]";
		mes "Bagus!";
		mes "Sekarang kita bisa memulai permainan ini!";
		mes "Aku akan melempar duluan~";
		break;
	}
	mes "^3355FF*Kocok.. kocok... Lempar!*^000000";
	next;
	set .@giveapple, .@amount*2;
	set .@table1, rand(1,6);
	set .@table2, rand(1,6);
	set .@tablesub, .@table1 + .@table2;
	set .@tabletotal, .@tablesub;
	mes "["+getarg(0)+"]";
	mes "Aku mendapat angka ^0000FF" + .@table1 + "^000000 dan ^0000FF" + .@table2 + "^000000.";
	mes "Jadi jumlahnya ^0000FF" + .@tablesub + "^000000.";
	mes "^FF0000" + strcharinfo(0) + "^000000, sekarang giliranmu.";
	next;
	select("Lempar dadu.");
	mes "^3355FF*kocok.. kocok... Lempar!*^000000";
	set .@player1, rand(1,6);
	set .@player2, rand(1,6);
	set .@playersub, .@player1 + .@player2;
	if (.@playersub > 9 && .@amount > 39) {
		set .@player1, rand(1,6);
		set .@player2, rand(1,6);
		set .@playersub, .@player1 + .@player2;
	}
	set .@playertotal, .@playersub;
	next;
	mes "["+getarg(0)+"]";
	mes "^FF0000" + strcharinfo(0) + "^000000, kamu mendapat angka ^FF0000" + .@player1 + "^000000 dan ^FF0000" + .@player2 + "^000000. Jadi jumlahnya ^FF0000" + .@playersub + "^000000 .";
	next;
	mes "["+getarg(0)+"]";
	if(.@playersub == .@tablesub) {
		mes "Saat ini jumlah angkaku adalah ^0000FF" + .@tablesub + "^000000 dan ^FF0000" + strcharinfo(0) + "^000000, jumlah angkamu adalah ^FF0000" + .@playersub + "^000000. Saat ini kita seri. Apakah kamu ingin melempar dadu ketiga?";
	} else if (.@playersub > .@tablesub) {
		mes "Saat ini jumlah angkaku adalah ^0000FF" + .@tablesub + "^000000 dan ^FF0000" + strcharinfo(0) + "^000000, jumlah angkamu adalah ^FF0000" + .@playersub + "^000000. ^FF0000" + strcharinfo(0) + "^000000, jumlah angkamu lebih besar. Apakah kamu ingin melempar dadu ketiga?";
	} else if(.@tablesub > .@playersub) {
		mes "Saat ini jumlah angkaku adalah ^0000FF" + .@tablesub + "^000000 dan ^FF0000" + strcharinfo(0) + "^000000, jumlah angkamu adalah ^FF0000" + .@playersub + "^000000. jumlah angkaku lebih besar. Apakah kamu ingin melempar dadu ketiga?";
	}
	next;
	switch (select("Lempar dadu.:batal.")) {
	case 1:
		mes "^3355FF*Kocok.. kocok... Lempar!*^000000";
		set .@player3, rand(1,6);
		set .@playertotal, .@playertotal + .@player3;
		next;
		mes "["+getarg(0)+"]";
		if (.@playertotal > 12) {
			mes "^FF0000" + strcharinfo(0) + "^000000, kamu mendapat angka ^FF0000" + .@player3 + "^000000 dan sekarang jumlahnya adalah ^FF0000" + .@playertotal + "^000000. Maaf, kamu kalah. Ayo coba lagi!";
			close;
		}
		else if (.@playertotal < .@tablesub) {
			mes "^FF0000" + strcharinfo(0) + "^000000, kamu mendapat angka ^FF0000" + .@player3 + "^000000 dan sekarang jumlahnya adalah ^FF0000" + .@playertotal + "^000000. Jumlah angkamu tetap lebih kecil daripada jumlah angkaku. Maaf, kamu kalah. Ayo coba lagi!";
			close;
		}
		else if (.@playertotal == .@tablesub) {
			if (.@tablesub > 8) {
				mes "^FF0000" + strcharinfo(0) + "^000000, kamu mendapat angka ^FF0000" + .@player3 + "^000000 dan sekarang jumlahnya adalah ^FF0000" + .@playertotal + "^000000. aku tidak akan melempar dadu ketiga, jadi kita seri. Bermainlah lagi kapan-kapan~";
				close2;
				getitem 512,.@amount; //Apple
				end;
			}
		}
		else {
			mes "^FF0000" + strcharinfo(0) + "^000000, kamu mendapat angka ^FF0000" + .@player3 + "^000000 dan sekarang jumlahnya adalah ^FF0000" + .@playertotal + "^000000. Sekarang giliranku untuk melempar.";
		}
		break;
	case 2:
		mes "["+getarg(0)+"]";
		if (.@playersub > .@tablesub) {
			mes "Baiklah, kelihatannya kamu tidak ingin mengambil resiko. Aku akan melemparkan dadu ketiga.";
		}
		else if (.@playersub == .@tablesub) {
			if (.@tablesub > 8) {
				mes "Baiklah, kelihatannya kamu tidak ingin mengambil resiko. Aku tidak akan melempar dadu ketiga. Jadi kita seri, bermainlah lagi kapan-kapan~";
				close2;
				getitem 512,.@amount; //Apple
				end;
			}
			mes "Baiklah.";
			mes "Aku akan melempar lagi.";
		}
		else {
			mes "Tidak ada salahnya mencoba.";
			mes "Kelihatannya kali ini aku yang menang.";
			mes "Maaf ya~";
			close;
		}
		break;
	}
	next;
	mes "^3355FF*Kocok.. kocok... Lempar!*^000000";
	set .@table3, rand(1,6);
	set .@tabletotal, .@tabletotal + .@table3;
	next;
	mes "["+getarg(0)+"]";
	if (.@tabletotal > 12) {
		mes "Aku mendapat angka ^0000FF" + .@table3 + "^000000 dan sekarang jumlahnya adalah ^0000FF" + .@tabletotal + "^000000. Aku kalah karena jumlah angkaku lebih dari  12. Ambillah Apel ini. Sungguh permainan yang bagus!";
		close2;
		getitem 512,.@giveapple; //Apple
		end;
	}
	else if (.@playertotal > .@tabletotal) {
		mes "Aku mendapat angka ^0000FF" + .@table3 + "^000000 dan sekarang jumlahnya adalah ^0000FF" + .@tabletotal + "^000000. Dengan jumlah angka ^FF0000" + .@playertotal + "^000000 kamu memenangkan ronde ini, ^FF0000" + strcharinfo(0) + "^000000. Ambillah Apel ini, dan bermainlah lagi denganku kapan-kapan.";
		close2;
		getitem 512,.@giveapple; //Apple
		end;
	}
	else if (.@playertotal == .@tabletotal) {
		mes "Aku mendapat angka ^0000FF" + .@table3 + "^000000 dan sekarang jumlahnya adalah ^0000FF" + .@tabletotal + "^000000. Dengan jumlah angka ^FF0000" + .@playertotal + "^000000 ternyata kita seri, ^FF0000" + strcharinfo(0) + "^000000. Ambillah Apelmu ini. Ayo bermain lagi~";
		close2;
		getitem 512,.@amount; //Apple
		end;
	}
	else if (.@playertotal < .@tabletotal) {
		mes "Aku mendapat angka ^0000FF" + .@table3 + "^000000 dan sekarang jumlahnya adalah ^0000FF" + .@tabletotal + "^000000. Dengan jumlah angka ^FF0000" + .@playertotal + "^000000 kamu kalah di ronde ini, ^FF0000" + strcharinfo(0) + "^000000. Maafkan aku ya, tapi bermainlah lagi denganku kapan-kapan.";
		close;
	}
}
