//===== idAthena Script ======================================
//= Staff Bandara Einbroch
//===== Script Asli dari =====================================
//= rAthena
//===== Diterjemahkan Oleh ===================================
//= Tim Penerjemah idAthena
//===== Oleh: ================================================
//= L0ne_W0lf, Muad_Dib
//===== Versi Saat Ini: ======================================
//= 1.0
//===== Dapat Digunakan Dengan: ==============================
//= rAthena SVN
//= idAthena SVN
//===== Deskripsi: ===========================================
//= Staff Bandara Einbroch
//===== Komentar Tambahan: ===================================
//= 1.0 Versi pertama.
//============================================================

airport,143,43,5	script	Airport Staff#airport1a::airport1	90,{
	mes "[Staff Bandara]";
	mes "Selamat datang di Bandara Einbroch,";
	mes "dimana kita menawarkan penerbangan tiada henti ke Juno, Lighthalzen, dan Hugel.";
	next;
	if (select("Menaiki Kapal Udara:Batal") == 1) {
		mes "[Staff Bandara]";
		mes "Tarif penerbangan Kapal Udara adalah 1,200 zeny,";
		mes "tetapi jika kamu memiliki Free Ticket for Airship, biaya akan gratis.";
		mes "Akankah kamu menaiki Kapal Udara?";
		next;
		if (select("Iya:Tidak") == 1) {
			if (countitem(7311) > 0) {
				delitem 7311,1; //Free_Flying_Ship_Ticket
				warp "airport",148,51;
				end;
			}
			if (Zeny >= 1200) {
				set Zeny, Zeny - 1200;
				warp "airport",148,51;
				end;
			}
			mes "[Staff Bandara]";
			mes "Mohon maaf, tetapi kamu tidak memiliki tiket untuk Kapal Udara";
			mes "dan kamu tidak memiliki cukup zeny untuk biaya penerbangan.";
			mes "Perlu diingat, biaya penerbangan adalah 1,200 Zeny";
			close;
		}
	}
	mes "[Staff Bandara]";
	mes "Terimakasih dan semoga menjadi hari yang indah.";
	close;
}

airport,158,43,5	duplicate(airport1)	Airport Staff#airport1b	90
airport,126,43,5	duplicate(airport1)	Airport Staff#airport1c	90

airport,143,49,3	script	Arrival Staff#airport2a::airport2	90,{
	mes "[Staff Kedatangan]";
	mes "Selamat datang di Bandara Einbroch.";
	mes "Jika kamu tiba dari penerbanganmu, izinkan saya menunjukkan terminal utama.";
	mes "Atau, mohon menaiki Kapal Udara ke Juno, Lighthalzen, dan Hugel.";
	next;
	if (select("Keluar ke terminal utama:Batal") == 1) {
		mes "[Staff Kedatangan]";
		mes "Setibanya kamu di terminal utama, kamu akan dikenakan biaya lagi untuk menaiki kapal udara.";
		mes "Kamu lebih baik keluar jika Einbroch adalah kota tujuanmu.";
		mes "Lanjutkan ke terminal utama?";
		next;
		if (select("Iya:Tidak") == 1) {
			warp "airport",142,40;
			end;
		}
	}
	mes "[Staff Kedatangan]";
	mes "Baiklah, terimakasih telah terbang bersama kami dan semoga selamat menikmati penerbangan~";
	close;
}

airport,126,51,3	duplicate(airport2)	Arrival Staff#airport2b	90
airport,158,50,3	duplicate(airport2)	Arrival Staff#airport2c	90

einbroch,94,267,3	script	Airship Staff#ein01	91,{
	mes "[Staff Kapal Udara]";
	mes "Selamat datang di bandara Juno.";
	mes "Silahkan gunakan pintu ini untuk";
	mes "naik pesawat yang akan";
	mes "terbang ke Izlude";
	mes "di Kerajaan Rune Midgard,";
	mes "dan Rachel di";
	mes "Republik Arunafeltz.";
	next;
	mes "[Staff Kapal Udara]";
	mes "Sebaliknya, Jika Juno";
	mes "adalah tujuan yang kamu maksud,";
	mes "silahkan turuni tangga";
	mes "dan tanya staff kami untuk membimbing kamu";
	mes "ke terminal utama. terima kasih,";
	mes "dan selamat menikmati perjalanannya.";
	close;
}
